# 2n: Functional Hacks {#2n-functional-hacks}

## `++aftr` {#aftr}

Pair after

`+aftr` first takes gate `a`, producing a wet gate. The new wet gate then takes `b`, producing the inverted pair of `[b a]`. This is the inverted version of [+fore](#fore).

#### Accepts {#accepts}

`a` is a gate, and the sample of `+aftr`.

`b` is a gate, and the sample of `(aftr a)`.

#### Produces {#produces}

`(pair b a)`.

#### Source {#source}

```hoon
++  aftr  |*(a=$-(* *) |*(b=$-(* *) (pair b a)))
```

#### Examples {#examples}

```
> =a (aftr @ud)
> `(a @t)`['foo' 42]
[p='foo' q=42]
```

---

## `++cork` {#cork}

Compose forward

Call gate `a`, then call gate `b` with its product.

This is a wet gate that takes two gates and produces a new gate.

This is the inverse of [`++corl`](#corl).

#### Accepts {#accepts}

`a` is a gate.

`b` is a gate.

#### Source {#source}

```hoon
++  cork  |*([a=$-(* *) b=$-(* *)] (corl b a))
```

#### Examples {#examples}

```
> ((cork dec |=(a=@ [a a])) 20)
[19 19]

> ((cork dec some) 20)
[~ u=19]
```

---

## `++corl` {#corl}

Compose backward

Call gate `b`, then call gate `a` with its product.

This is a wet gate that takes two gates and produces a new gate.

This is the inverse of [`++cork`](#cork).

#### Accepts {#accepts}

`a` is a gate.

`b` is a gate.

#### Source {#source}

```hoon
++  corl
  |*  [a=$-(* *) b=$-(* *)]
  =<  +:|.((a (b)))      ::  type check
  =+  c=+<.b
  |@  ++  $  (a (b c))
  --
```

#### Examples {#examples}

```
> ((corl |=(a=@ [a a]) dec) 20)
[19 19]

> ((corl some dec) 20)
[~ u=19]
```

---

## `++curr` {#curry}

Right curry

Right-curry a gate, binding the tail of its sample

#### Accepts {#accepts}

`a` is a gate.

`c` is a noun.

#### Produces {#produces}

A gate.

#### Source {#source}

```hoon
++  curr
  |*  [a=$-(^ *) c=*]
  =+  b=+<+.a
  |@  ++  $  (a b c)
  --
```

#### Examples {#examples}

```
    > =tep (curr scan sym)
    > `@t`(tep "asd")
    'asd'

    > `@t`(tep "lek-om")
    'lek-om'
```

---

## `++cury` {#cury}

Curry left

Curry a gate, binding the head of its sample

#### Accepts {#accepts}

`a` is a gate.

`b` is a noun.

#### Produces {#produces}

A gate.

#### Source {#source}

```hoon
++  cury
  |*  [a=$-(^ *) b=*]
  =+  c=+<+.a
  |@  ++  $  (a b c)
  --
```

#### Examples {#examples}

```
    > =mol (cury add 2)
    > (mol 4)
    6

    > (mol 7)
    9
```

---

## `++fore` {#fore}

Pair before

`+fore` first takes gate `a`, producing a wet gate. The new wet gate then takes `b`, producing the pair of `[a b]`.

#### Accepts {#accepts}

`a` is a gate, and is the sample of `+fore`.

`b` is a gate, and is the sample of `(fore a)`.

#### Produces {#produces}

`(pair a b)`.

#### Source {#source}

```hoon
++  fore  |*(a=$-(* *) |*(b=$-(* *) (pair a b)))
```

#### Examples {#examples}

```
> =a (fore @ud)
> `(a @t)`[42 'foo']
[p=42 q='foo']
```

---

## `++head` {#head}

Get head

Produces the head of a cell.

#### Accepts {#accepts}

A cell.

#### Produces {#produces}

A noun.

#### Source {#source}

```hoon
++  head  |*(^ ,:+<-)
```

#### Examples {#examples}

```
> (head [1 2])
1

> (head [[1 1] 2])
[1 1]

> (head "hello")
'h'
```

---

## `++same` {#same}

Identity

Produces the same value that it was given.

#### Accepts {#accepts}

A noun.

#### Produces {#produces}

A noun.

#### Source {#source}

```hoon
++  same  |*(* +<)
```

#### Examples {#examples}

```
> (same [1 2])
[1 2]

> (same [[1 1] 2])
[[1 1] 2]

> (same "hello")
"hello"
```

---

## `++succ` {#succ}

Successor

Increment an atom.

#### Accepts {#accepts}

An `atom`.

#### Produces {#produces}

An `atom`.

#### Source {#source}

```hoon
++  succ  |=(@ +(+<))
```

#### Examples {#examples}

```
> (succ 1)
2
```

---

## `++tail` {#tail}

Get tail

Produces the tail of a cell.

#### Accepts {#accepts}

A cell.

#### Produces {#produces}

A noun.

#### Source {#source}

```hoon
++  tail  |*(^ ,:+<+)
```

#### Examples {#examples}

```
    > (tail [1 2])
    2

    > (tail [[1 1] 2])
    2

    > (tail "hello")
    "ello"
```

---

## `++test` {#test}

Test for equality

Checks if `a` and `b` are equal, producing a flag.

#### Accepts {#accepts}

`a` is a noun.

`b` is a noun.

#### Produces {#produces}

A flag.

#### Source {#source}

```hoon
++  test  |=(^ =(+<- +<+))
```

#### Examples {#examples}

```
    > (test 1 1)
    %.y

    > (test [2 0] 2)
    %.n

    > (test "hello" 'hello')
    %.n

    > (test "hello" ['h' 'e' 'l' 'l' 'o' ~])
    %.y
```

---

## `++lead` {#lead}

Put head

`(lead a)` produces a wet gate, then `((lead a) b)` produces `[a b]`.

#### Accepts {#accepts}

`a` is a noun, and is the sample of `+lead`.

`b` is a noun, and is the sample of `((lead a) b)`

#### Produces {#produces}

A cell of `[a b]`.

#### Source {#source}

```hoon
++  lead  |*(* |*(* [+>+< +<]))
```

#### Examples {#examples}

```
> =a (lead 'foo')
> (a 'bar')
['foo' 'bar']
```

---

## `++late` {#late}

Put tail

`(late a)` produces a wet gate, then `((late a) b)` produces the inverted cell `[b a]`. This is the inverted version of [+lead](#lead).

#### Accepts {#accepts}

`a` is a noun, and is the sample of `+late`.

`b` is a noun, and is the sample of `(late a)`.

#### Produces {#produces}

A cell of `[b a]`.

#### Source {#source}

```hoon
++  late  |*(* |*(* [+< +>+<]))
```

#### Examples {#examples}

```
> =a (late 'foo')
> (a 'bar')
['bar' 'foo']
```

---

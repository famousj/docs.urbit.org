# 2m: Container from Noun {#2m-container-from-noun}

## `++ly` {#ly}

List from raw noun

Takes a null-terminated noun and produces a `list`.

#### Accepts {#accepts}

`a` is a null-terminated noun.

#### Produces {#produces}

A `list`.

#### Source {#source}

```hoon
    ++  ly
      le:nl
```

#### Examples {#examples}

```
    > (ly [1 2 3 ~])
    ~[1 2 3]

    > (ly "abc")
    ~['a' 'b' 'c']
```

#### Discussion {#discussion}

`ly` is an alias for `le:nl`.

---

## `++my` {#my}

Map from raw noun

Takes a null-terminated noun and produces a `map.`

#### Accepts {#accepts}

`a` is a noun composed of ordered pairs and terminated with `~`.

#### Produces {#produces}

A `map`.

#### Source {#source}

```hoon
    ++  my
      my:nl
```

#### Examples {#examples}

```
    > (my [['a' 1] ['b' 2] ~])
    [n=[p='b' q=2] l={[p='a' q=1]} r={}]
```

#### Discussion {#discussion}

`my` is an alias for `my:nl`.

---

## `++sy` {#sy}

Set from raw noun

Takes a null-terminated noun and produces a `set`.

#### Accepts {#accepts}

`a` is a null-terminated noun.

#### Produces {#produces}

A `set`.

#### Source {#source}

```hoon
    ++  sy
      si:nl
```

#### Examples {#examples}

```
    > (sy [1 2 3 ~])
    [n=2 l={1 3} r={}]

    > (sy (ly [1 2 3 ~]))
    [n=2 l={1 3} r={}]

    > (sy "abc")
    [n='b' l={'a' 'c'} r={}]
```

#### Discussion {#discussion}

`sy` is an alias for `si:nl`.

---

## `++nl` {#nl}

Noun-to-container operations

Core whose arms contain functions that create various typed nouns from raw
nouns.

#### Source {#source}

```hoon
    ++  nl
      |%
```

---

### `++le:nl` {#lenl}

Construct list

Takes a null-terminated noun and produces a `list`.

#### Accepts {#accepts}

`a` is a null-terminated noun.

#### Produces {#produces}

A list.

#### Source {#source}

```hoon
++  le
  |*  a=(list)
  ^+  =<  $
    |@  ++  $  ?:(*? ~ [i=(snag 0 a) t=$])
    --
  a
```

#### Examples {#examples}

```
    > (le:nl [1 2 3 ~])
    ~[1 2 3]

    > (le:nl "abc")
    ~['a' 'b' 'c']
```

---

### `++my:nl` {#mynl}

Construct map

Takes a null-terminated noun and produces a `map` of the same type of the `p` and `q` passed in.

#### Accepts {#accepts}

`a` is a noun composed of ordered pairs and terminated with `~`.

#### Produces {#produces}

A map.

#### Source {#source}

```hoon
++  my
  |*  a=(list (pair))
  =>  .(a ^+((le a) a))
  (~(gas by `(map _p.i.-.a _q.i.-.a)`~) a)
```

#### Examples {#examples}

```
    > (my:nl [['a' 1] ['b' 2] ~])
    [n=[p='b' q=2] l={[p='a' q=1]} r={}]
```

---

### `++si:nl` {#sinl}

Construct set

Takes a null-terminated noun and produces a `set`.

#### Accepts {#accepts}

`a` is a null-terminated noun.

#### Produces {#produces}

A set.

#### Source {#source}

```hoon
++  si
  |*  a=(list)
  =>  .(a ^+((le a) a))
  (~(gas in `(set _i.-.a)`~) a)
```

#### Examples {#examples}

```
    > (si:nl `(list [@t *])`[['a' 1] ['b' [2 3]] ~])
    [n=['b' [2 3]] l={['a' 1]} r={}]
```

---

### `++snag:nl` {#snagnl}

Index

Produces the element at the index `a` of null-terminated noun `b` and failing if the noun is null. Lists are 0-indexed.

#### Accepts {#accepts}

`a` is a noun.

`b` is a null-terminated noun.

#### Produces {#produces}

A noun.

#### Sources {#sources}

```hoon
++  snag
  |*  [a=@ b=(list)]
  ?~  b
    ~_  leaf+"snag-fail"
    !!
  ?:  =(0 a)  i.b
  $(b t.b, a (dec a))
```

#### Examples {#examples}

```
    > =b [[2 3] [1 4] ~]
    > (snag:nl 0 b)
    [2 3]

    > (snag:nl 2 b)
    ! snag-fail
    ! exit
```

---

### `++weld:nl` {#weldnl}

Concatenate

Produces a list that is the concatenation of null-terminated nouns `a` and `b`.

#### Accepts {#accepts}

`a` is a null-terminated noun.

`b` is a null-terminated noun.

#### Produces {#produces}

A list.

#### Source {#source}

```hoon
++  weld
  |*  [a=(list) b=(list)]
  =>  .(a ^+((le a) a), b ^+((le b) b))
  =+  42
  |-
  ?~  a  b
  [i=i.a t=$(a t.a)]
```

#### Examples {#examples}

```
    > =b [[2 3] [1 4] ~]

    > (weld:nl b [8 9 ~])
    [i=[2 3] t=[i=[1 4] t=~[8 9]]]
```

---

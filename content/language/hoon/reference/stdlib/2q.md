# 2q: Molds and Mold-Builders {#2q-molds-and-mold-builders}

## `+$axis` {#axis}

Tree address

A Nock axis inside a noun. After the leading 1, in binary, a `1` signfies right and `0` left.

#### Source {#source}

```hoon
+$  axis  @
```

#### Examples {#examples}

```
> =axis `axis`7
> axis
7
> `@ub`axis
0b111

> =noun [[4 5] [6 14 15]]

> .*(noun [0 axis])
[14 15]
```

---

## `+$bean` {#bean}

Boolean

`0`, `&`, or `%.y` are true, and `1`, `|`, and `%.n` are false.

Note 1 is false and 0 is true. This is sometimes referred to as "loobean".

#### Source {#source}

```hoon
+$  bean  ?
```

#### Examples {#examples}

```
> *bean
%.y
```

```
> `bean`&
%.y
```

```
> `bean`|
%.n
```

---

## `+$flag` {#flag}

Boolean

Same as [$bean](#bean).

#### Source {#source}

```hoon
+$  flag  ?
```

---

## `+$char` {#char}

Character

A single `@t` character.

#### Source {#source}

```hoon
+$  char  @t
```

#### Examples {#examples}

```
> *char
''
```

```
> `char`'a'
'a'
```

---

## `+$cord` {#cord}

UTF-8 text

One of Hoon's two string types (the other being `tape`). A cord is an atom of UTF-8 text.

#### Source {#source}

```hoon
+$  cord  @t
```

#### Examples {#examples}

```
> *cord
''

> `@ux`'foobar'
0x7261.626f.6f66

> `@t`97
'a'

> `@`'urbit'
499.984.265.845

> `@t`499.984.265.845
'urbit'
```

#### Discussion {#discussion}

Aura `@t` designates a Unicode atom, little-endian: the first character in the text is the least-significant byte.

[`trip`](4b.md#trip) converts from `cord` to `tape`, and [`crip`](4b.md#crip) converts from `tape` to `cord`.

---

## `+$byts` {#byts}

bytes, LSB first

An atom `dat` with its byte-length specified in `wid`. This is to handle leading zeros that are typically ignored.

#### Source {#source}

```hoon
+$  byts  [wid=@ud dat=@]
```

#### Examples {#examples}

```
> *byts
[wid=0 dat=0]
```

```
> =a `byts`[4 2]
> a
[wid=4 dat=2]

> `@ux`(rev 3 a)
0x200.0000
```

---

## `+$date` {#date}

Parsed date

A boolean designating AD or BC, a year atom, a month atom, and a `tarp`, which is a day atom and a time.

#### Source {#source}

```hoon
+$  date  [[a=? y=@ud] m=@ud t=tarp]
```

#### Examples {#examples}

```
> *date
[[a=%.y y=0] m=0 t=[d=0 h=0 m=0 s=0 f=~]]

> `date`(yore ~2014.6.6..21.09.15..0a16)
[[a=%.y y=2.014] m=6 t=[d=6 h=21 m=9 s=15 f=~[0xa16]]]

> now
~2018.5.25..17.55.15..9ad8
```

#### Discussion {#discussion}

See also: [`year`](3c.md#year), [`yore`](3c.md#yore)

---

## `+$knot` {#knot}

URL-safe path element text

An atom type that only permits lower-case letters, numbers, and four special characters: hyphen, tilde, underscore and period.

#### Source {#source}

```hoon
+$  knot  @ta
```

#### Examples {#examples}

```
> *knot
~.

> `@ta`105
~.i
```

---

## `+$noun` {#noun}

Any noun

#### Source {#source}

```hoon
+$  noun  *
```

#### Examples {#examples}

```
> *noun
0
```

```
> `noun`'foo'
7.303.014
```

```
> `noun`"foo"
[102 111 111 0]
```

---

## `+$path` {#path}

Like unix path

A path is a list of [`$knot`](#knot).

#### Source {#source}

```hoon
+$  path  (list knot)
```

#### Examples {#examples}

```
> *path
/
```

```
> `path`"foo"
/f/o/o
```

```
> `path`[~.foo ~.bar ~.baz ~]
/foo/bar/baz
```

---

## `+$stud` {#stud}

Standard name

#### Source {#source}

```hoon
+$  stud                    ::  standard name
          $@  mark=@tas     ::  auth=urbit
          $:  auth=@tas     ::  standards authority
              type=path     ::  standard label
          ==                ::
```

---

## `+$tang` {#tang}

Bottom-first error

A list of [`tank`](#tank)s. It's for printing types and bottom-up printing of stack traces.

#### Source {#source}

```hoon
+$  tang  (list tank)
```

#### Examples {#examples}

```
> *tang
~
```

```
> %-  %-  slog
      ^-  tang
      :~  'foo'
          leaf+"bar"
          [%palm ["|" "(" "!" ")"] leaf+"foo" leaf+"bar" leaf+"baz" ~]
          [%rose [" " "[" "]"] leaf+"foo" leaf+"bar" leaf+"baz" ~]
      ==
foo
bar
(!foo|bar|baz)
[foo bar baz]
  ~
~
```

---

## `+$tank` {#tank}

Formatted print tree

A `tank` is one of four cases:

- just a `cord`.
- `%leaf` is just a `tape`.
- `%palm` is a list of `tank` delimited by the strings in `p`, with back-steps at new lines.
- `%rose` is a list of `tank` delimited by the strings in `p` without back-steps at new lines.

#### Source {#source}

```hoon
+$  tank
  $~  leaf/~
  $@  cord
  $%  [%leaf p=tape]
      [%palm p=(qual tape tape tape tape) q=(list tank)]
      [%rose p=(trel tape tape tape) q=(list tank)]
  ==
```

#### Examples {#examples}

```
> *tank
[%leaf p=""]
```

```
> ~(ram re 'foobar')
"foobar"
```

```
> ~(ram re leaf+"foo")
"foo"
```

```
> ~(ram re [%palm ["|" "(" "!" ")"] leaf+"foo" leaf+"bar" leaf+"baz" ~])
"(!foo|bar|baz)"
```

```
> ~(ram re [%rose [" " "[" "]"] leaf+"foo" leaf+"bar" leaf+"baz" ~])
"[foo bar baz]"
```

---

## `+$tape` {#tape}

List of characters

One of Hoon's two string types, the other being [`+cord`](#cord). A tape is a list of `@tD`.

#### Source {#source}

```hoon
+$  tape  (list @tD)
```

#### Examples {#examples}

```
> *tape
""

> `tape`"foobar"
"foobar"

>`(list @t)`"foobar"
<|f o o b a r|>

>`(list @tD)`"foobar"
"foobar"

> `(list @)`"foobar"
~[102 111 111 98 97 114]
```

---

## `+$tour` {#tour}

UTF-32 clusters

A `tour` is a list of UTF-32 characters.

#### Source {#source}

```hoon
+$  tour  (list @c)
```

#### Examples {#examples}

```
> *tour
~
```

```
> `tour`(tuba "foobar")
~[~-f ~-o ~-o ~-b ~-a ~-r]
```

---

## `+$tarp` {#tarp}

Parsed time

The time component of a `date`: day, hour, minute, second and a list of `@ux` for precision.

#### Source {#source}

```hoon
+$  tarp  [d=@ud h=@ud m=@ud s=@ud f=(list @ux)]
```

#### Examples {#examples}

```
> *tarp
[d=0 h=0 m=0 s=0 f=~]

> (yell now)
[d=106.751.991.821.625 h=22 m=58 s=10 f=~[0x44ff]]

> `tarp`(yell ~2014.6.6..21.09.15..0a16)
[d=106.751.991.820.172 h=21 m=9 s=15 f=~[0xa16]]

> (yell ~d20)
[d=20 h=0 m=0 s=0 f=~]
```

---

## `+$term` {#term}

Hoon constant

A restricted text atom for Hoon constants. The only characters permitted are lowercase ASCII letters, `-`, and `0-9`, the latter two of which cannot be the first character. The syntax for `@tas` is the text itself, always preceded by `%`. The empty `@tas` has a special syntax, `$`.

#### Source {#source}

```hoon
+$  term  @tas
```

#### Examples {#examples}

```
> *term
%$

> *%term
%term

> %dead-fish9
%dead-fish9

> -:!>(%dead-fish9)
#t/%dead-fish9

> -:!>(`@tas`%dead-fish9)
#t/@tas
```

---

## `+$wain` {#wain}

List of strings

A list of `cord`s. A `wain` is used instead of a single `cord` with `\n`.

#### Source {#source}

```hoon
+$  wain  (list cord)
```

#### Examples {#examples}

```
> *wain
<||>

> `wain`/som/del/rok
<|som del rok|>
```

---

## `+$wall` {#wall}

List of list of characters

A list of `tape`s. `wall` is used instead of a single `tape` with `\n`.

#### Source {#source}

```hoon
+$  wall  (list tape)
```

#### Examples {#examples}

```
    > *wall
    <<>>

    > `wall`(wash [0 20] leaf+<(bex 256)>)
    <<
      "\\/115.792.089.237.\\/"
      "  316.195.423.570."
      "  985.008.687.907."
      "  853.269.984.665."
      "  640.564.039.457."
      "  584.007.913.129."
      "  639.936"
      "\\/                \\/"
    >>
```

---

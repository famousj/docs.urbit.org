# 5e: Molds and mold builders {#5e-molds-and-mold-builders}

## `+$mane` {#mane}

XML name+space

XML tag name and optional namespace.

#### Source {#source}

```hoon
+$  mane  $@(@tas [@tas @tas])
```

#### Examples {#examples}

```
> (en-xml:html ;foo;)
"<foo></foo>"

> (en-xml:html ;foo_bar;)
"<foo:bar></foo:bar>"

> `manx`;foo_bar;
[g=[n=[%foo %bar] a=~] c=~]

> `mane`n.g:`manx`;foo_bar;
[%foo %bar]

> `mane`n.g:`manx`;foo;
%foo
```

---

## `+$manx` {#manx}

Dynamic XML node

An XML element which may contain text, attributes, and other elements.

`g` is a [`$marx`](#marx) (a tag) and `c` is a [`$marl`](#marl) (its contents).

#### Source {#source}

```hoon
+$  manx  $~([[%$ ~] ~] [g=marx c=marl])
```

#### Examples {#examples}

```
> *manx
[g=[n=%$ a=~] c=~

> `manx`;foo;
[g=[n=%foo a=~] c=~]

> (en-xml:html `manx`;foo;)
"<foo></foo>"

> =a ^-  manx
     ;foo
       ;bar: abc
       ;baz
         ;xxx: hello
       ==
     ==

> a
[ g=[n=%foo a=~]
    c
  ~[
    [ g=[n=%bar a=~]
      c=~[[g=[n=%$ a=~[[n=%$ v="abc"]]] c=~]]
    ]
    [ g=[n=%baz a=~]
        c
      ~[
        [ g=[n=%xxx a=~]
          c=~[[g=[n=%$ a=~[[n=%$ v="hello"]]] c=~]]
        ]
      ]
    ]
  ]
]

> (en-xml:html a)
"<foo><bar>abc</bar><baz><xxx>hello</xxx></baz></foo>"
```

---

## `+$marl` {#marl}

XML node list

A list of XML nodes ([`$marx`](#marx)).

#### Source {#source}

```hoon
+$  marl  (list manx)
```

#### Examples {#examples}

```
> *marl
~

> ^-  marl
  ;=
    ;foo: abc
    ;bar: def
  ==
~[
  [g=[n=%foo a=~] c=~[[g=[n=%$ a=~[[n=%$ v=~['a' 'b' 'c']]]] c=~]]]
  [g=[n=%bar a=~] c=~[[g=[n=%$ a=~[[n=%$ v=~['d' 'e' 'f']]]] c=~]]]
]

> %-  en-xml:html
  ;baz
    ;=
      ;foo: abc
      ;bar: def
    ==
  ==
"<baz><foo>abc</foo><bar>def</bar></baz>"
```

---

## `+$mars` {#mars}

XML cdata

#### Source {#source}

```hoon
+$  mars  [t=[n=%$ a=[i=[n=%$ v=tape] t=~]] c=~]
```

---

## `+$mart` {#mart}

XML attributes

A list of atributes for an XML tag. For each list item, `n` is a [`$mane`](#mane) (an attribute name with optional namespace) and `v` is a `tape` (the attribute itself).

#### Source {#source}

```hoon
+$  mart  (list [n=mane v=tape])
```

#### Examples {#examples}

```
> *mart
~

> `manx`;foo.bar;
[g=[n=%foo a=~[[n=%class v="bar"]]] c=~]

> `mart`a.g:`manx`;foo.bar;
~[[n=%class v="bar"]]

> (en-xml:html ;foo.bar;)
"<foo class=\"bar\"></foo>"
```

---

## `+$marx` {#marx}

Dynamic XML tag

An XML tag with optional attributes. `n` is a [`$mane`](#mane) (the tag name with optional namespace) and `a` is a [`$mart`](#mart) (any XML attributes).

#### Source {#source}

```hoon
+$  marx  $~([%$ ~] [n=mane a=mart])
```

#### Examples {#examples}

```
> `manx`;foo.bar;
[g=[n=%foo a=~[[n=%class v="bar"]]] c=~]

> `marx`g:`manx`;foo.bar;
[n=%foo a=~[[n=%class v="bar"]]]

> (en-xml:html ;foo.bar;)
"<foo class=\"bar\"></foo>"
```

---

## `+$mite` {#mite}

MIME type

This type represents a MIME type like `text/plain` as a `path`.

#### Source {#source}

```hoon
+$  mite  (list @ta)
```

#### Examples {#examples}

```
> `mite`/text/plain
/text/plain
```

---

## `+$pass` {#pass}

Public key

This type is used for a ship's public key, as an atom.

#### Source {#source}

```hoon
+$  pass  @
```

---

## `+$ring` {#ring}

Private key

This type is used for a ship's private key, as an atom.

#### Source {#source}

```hoon
+$  ring  @
```

---

## `+$ship` {#ship}

Network identity

Just an `@p`

#### Source {#source}

```hoon
+$  ship  @p
```

#### Examples {#examples}

```
> *ship
~zod

> `ship`~sampel-palnet
~sampel-palnet
```

---

## `+$shop` {#shop}

Urbit/DNS identity

Either a [`$ship`](#ship) or a domain name as a `path`.

#### Source {#source}

```hoon
+$  shop  (each ship (list @ta))
```

---

## `+$spur` {#spur}

ship desk case spur

The part of a Clay `path` after the `%`.

#### Source {#source}

```hoon
+$  spur  path
```

---

## `+$time` {#time}

Galactic time

Just a `@da`

#### Source {#source}

```hoon
+$  time  @da
```

#### Examples {#examples}

```
> *time
~2000.1.1
```

---
